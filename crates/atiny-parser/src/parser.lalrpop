use std::str::FromStr;

use atiny_tree::r#abstract::*;
use atiny_location::*;

grammar;

Loc<T>: Located<T> = {
    <s: @L> <data: T> <e: @R> => Located { data, location: ByteRange(Byte(s), Byte(e)) }
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        Some(e) => {
            v.push(e);
            v
        }
        None => v
    }
};

// For expressions

AtomItem<T>: AtomKind<T> = {
    <n:Num>            => AtomKind::Number(n),
    "true"             => AtomKind::Boolean(true),
    "false"            => AtomKind::Boolean(false),
    <id: Id>           => AtomKind::Identifier(id),
    "(" <vec:Comma<T>> ")" => {
        match vec.len() {
            0 => AtomKind::Unit,
            _ => AtomKind::Tuple(vec),
        }
    },
}

AtomExpr: Expr = {
    <a: Loc<AtomItem<Expr>>> => a.map(|a| ExprKind::Atom(a))
};

CallItem: ExprKind = {
    <t:Call> <a:AtomExpr> => ExprKind::Application(Box::new(t), Box::new(a)),
    <t:AtomExpr>          => t.data,
}

Call: Expr = Loc<CallItem>;

ExprItem: ExprKind = {
    "|" <t: Id> "|" <s:Expr>                     => ExprKind::Abstraction(t, Box::new(s)),
    "let" <t: Id> "=" <e:Expr> ";" <next:Expr>   => ExprKind::Let(t, Box::new(e), Box::new(next)),
    "match" <pat:Expr> "{" <c:Clauses> "}"       => ExprKind::Match(Box::new(pat), c),
    "if" "let" <pat:AtomPat> "=" <m:Expr>
        "{" <t:Expr> "}" "else"
        "{" <f:Expr> "}"                         => ExprKind::if_let(pat, m, t, f),
    "{" <t:Expr> "}"                             => t.data,
    <f:Call> ":" <t: Type>                       => ExprKind::Annotation(Box::new(f), Box::new(t)),
    <f:Call>                                     => f.data,
};

pub Expr : Expr = Loc<ExprItem>;

AtomPat: Pattern = {
    <a: Loc<AtomItem<Pattern>>> => a.map(|a| PatternKind::Atom(a))
};

Clause: Clause = {
    <pat:AtomPat> "=>" <e:Expr> => Clause::new(pat, e)
}

Clauses = Comma<Clause>;

PatternItem : PatternKind = {
    <pat:AtomPat> => pat.data
}

pub Pattern : Pattern = Loc<PatternItem>;

// For Types

TypeAtomItem : TypeKind = {
    <id: Id>                    => TypeKind::Variable(VariableNode { name: id }),
    "(" <types:Comma<Type>> ")" => {
        match types.len() {
            0 => TypeKind::Unit,
            _ => TypeKind::Tuple(TypeTupleNode { types }),
        }
    },
}

TypeAtom : Type = Loc<TypeAtomItem>;

TypeCallItem : TypeKind = {
    <t:TypeCall> <a:TypeAtom> => todo!(),
    <t:TypeAtom>  => t.data,
}

TypeCall : Type = Loc<TypeCallItem>;

TypeArrowItem : TypeKind = {
    <t:TypeCall> "->" <a:Type> => TypeKind::Arrow(ArrowNode { left: Box::new(t), right: Box::new(a) }),
    <t:TypeCall>               => t.data,
}

TypeArrow : Type = Loc<TypeArrowItem>;

TypeKind : TypeKind = {
    <t: TypeArrow> => t.data,
    "forall" <args: Id*> "." <body: Type> => TypeKind::Forall(ForallNode { args, body: Box::new(body) }),
}

Type : Type = Loc<TypeKind>;

Num: u64 = <s:r"[0-9]+">                   => u64::from_str(s).unwrap();
Id: String = <s:r"[a-zA-Z_][a-zA-Z0-9_]*"> => s.to_string();

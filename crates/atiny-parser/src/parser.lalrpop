use std::str::FromStr;

use atiny_tree::r#abstract::*;
use atiny_location::*;

grammar;

Loc<T>: Located<T> = {
    <s: @L> <data: T> <e: @R> => Located { data, location: ByteRange(Byte(s), Byte(e)) }
}

//         (Vec,Trailing)
Sep<U, T>: (Vec<T>, bool) = {
    <mut v:(<T> U)*> <e:T?> => match e {
        Some(e) => {
            v.push(e);
            (v, false)
        }
        None => (v, true)
    }
};

BlockList<U, T>: Vec<T> = "{" <Sep<U, T>> "}"  => <>.0;

// O: From<AtomKind<O>>
AtomItem<I, O> : O = {
    "_" => AtomKind::Wildcard.into(),
    Num => AtomKind::Number(<>).into(),
    Id  => AtomKind::Identifier(<>).into(),
    "(" <mut sep:Sep<",", I>> ")" => {
        let (mut vec, trailing) = sep;

        match vec.len() {
            0 => AtomKind::unit().into(),
            1 if !trailing => vec.pop().unwrap().data,
            _ => AtomKind::Tuple(vec).into(),
        }
    },
}

// For expressions

AtomExpr: Expr = Loc<AtomItem<Expr, ExprItem>>;

CallItem: ExprKind = <t:Call> <a:AtomExpr> => ExprKind::Application(Box::new(t), Box::new(a));

Call: Expr = {
    Loc<CallItem>,
    AtomExpr
};

ExprItem: ExprKind = {
    "|" <t:LowerId> "|" <s:Expr>                  => ExprKind::Abstraction(t, Box::new(s)),
    "match" <pat:Expr> <c:BlockList<",", Clause>> => ExprKind::Match(Box::new(pat), c),
    <f:Call> <t:TypeAnotation>                    => ExprKind::Annotation(Box::new(f), Box::new(t)),
    <t:Call> "#" <f:BlockList<",", ExprField>>    => ExprKind::RecordCreation(Box::new(t), f),
    <f:AtomExpr> "." <name:LowerId>               => ExprKind::Field(Box::new(f), name),
    
    "if" "let" <Pattern> "=" <Expr> <BlockExpr> "else" <BlockExpr> => ExprKind::if_let(<>),
};

Statement: Statement = {
    "let" <LowerId> "=" <Expr> => Statement::Let(<>),
    Expr => Statement::Expr(<>)
}

Block: Vec<Statement> = {
    Loc<r"[{][\s]*[}]"> => vec![Statement::Expr(<>.map(|_| ExprKind::Atom(AtomKind::unit())))],
    BlockList<";", Statement>
};

BlockExpr: Expr = Loc<Block> => <>.map(|block|  ExprKind::Block(block));

pub Expr: Expr = {
    Loc<ExprItem>,
    BlockExpr,
    Call
};

AtomPattern: Pattern = Loc<AtomItem<Pattern, PatternItem>>;

PatternItem: PatternKind = UpperId AtomPattern+ => PatternKind::Constructor(<>);

Pattern: Pattern = {
    Loc<PatternItem>,
    AtomPattern
};

Clause: Clause = <Pattern> "=>" <Expr> => Clause::new(<>);

ExprField: ExprField = <name: LowerId> "=" <expr: Expr> => ExprField {<>};

// For Types

TypeAtomItem: TypeKind = {
    <name: Id> => TypeKind::Variable(VariableNode {<>}),
    "(" <mut sep:Sep<",", TypeNode>> ")" => {
        let (mut types, trailing) = sep;

        match types.len() {
            0 => TypeKind::unit(),
            1 if !trailing => types.pop().unwrap().data,
            _ => TypeKind::Tuple(TypeTupleNode { types }),
        }
    }
}

TypeAtom: TypeNode = Loc<TypeAtomItem>;

TypeCallItem: TypeKind = {
    <fun:UpperId> <args:TypeAtom+> => TypeKind::Application(TypeApplicationNode {<>}),
}

TypeCall: TypeNode = {
    Loc<TypeCallItem>,
    TypeAtom
};

TypeArrowItem: TypeKind = <TypeCall> "->" <TypeNode> => TypeKind::Arrow(ArrowNode::new(<>));

TypeArrow: TypeNode = {
    Loc<TypeArrowItem>,
    TypeCall
};

TypeKind: TypeKind = {
    "forall" <args: LowerId*> "." <body: TypeNode> => {
        TypeKind::Forall(ForallNode { args, body: Box::new(body) })
    }
}

TypeNode: TypeNode = {
    Loc<TypeKind>,
    TypeArrow
};

// Top level

Constructor: Constructor = "|" <name: UpperId> <types: TypeAtom*> => Constructor {<>};

Field: Field = <name: LowerId> ":" <ty: TypeNode> => Field {<>};

TypeDeclKind: TypeDeclKind = {
    Constructor+          => TypeDeclKind::Sum(<>),
    BlockList<",", Field> => TypeDeclKind::Product(<>)
}

TypeDecl: TypeDecl = {
    "type" <name: UpperId> <params: LowerId*> "=" <constructors: TypeDeclKind> => TypeDecl {<>}
}

TypeAnotation: TypeNode = ":" <TypeNode>;

Param: (Pattern, TypeNode) = "(" <Pattern> <TypeAnotation> ")" => (<>);

FnDecl: FnDecl = "fn" <Loc<LowerId>> <Param*> <TypeAnotation?> <BlockExpr> => FnDecl::new(<>);

TopLevelKind: TopLevelKind = {
    FnDecl   => TopLevelKind::FnDecl(<>), 
    TypeDecl => TopLevelKind::TypeDecl(<>), 
}

TopLevel: TopLevel = Loc<TopLevelKind>;

pub Program: Vec<TopLevel> = TopLevel*;

Num: u64 = r"[0-9]+" => u64::from_str(<>).unwrap();

LowerId: String =  {
    r"[a-z]"               => <>.to_string(),
    r"[a-z_][a-zA-Z0-9_]+" => <>.to_string(),
}

UpperId: String = r"[A-Z][a-zA-Z0-9_]*" => <>.to_string();

Id: String = {
    LowerId,
    UpperId,
}
